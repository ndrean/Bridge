//! Parser for PostgreSQL logical replication protocol
const std = @import("std");
const array_mod = @import("array.zig");
const ArrayResult = array_mod.ArrayResult;
const ArrayElement = array_mod.ArrayElement;
const numeric_mod = @import("numeric.zig");
const utils = @import("utils.zig");
const PgOid = @import("pg_constants.zig").PgOid;

pub const log = std.log.scoped(.pgoutput);

/// pgoutput protocol parser
///
/// Parses PostgreSQL logical replication binary messages
///
/// Spec: https://www.postgresql.org/docs/current/protocol-logicalrep-message-formats.html
pub const PgOutputMessage = union(enum) {
    begin: BeginMessage,
    commit: CommitMessage,
    relation: RelationMessage,
    insert: InsertMessage,
    update: UpdateMessage,
    delete: DeleteMessage,
    origin: OriginMessage,
    type: TypeMessage,
    truncate: TruncateMessage,
};

/// pgoutput message BEGIN structures
pub const BeginMessage = struct {
    final_lsn: u64,
    timestamp: i64,
    xid: u32,
};

/// pgoutput message COMMIT structures
pub const CommitMessage = struct {
    flags: u8,
    commit_lsn: u64,
    end_lsn: u64,
    timestamp: i64,
};

/// Decoded column value representation
pub const DecodedValue = union(enum) {
    null,
    boolean: bool,
    int32: i32,
    int64: i64,
    float64: f64,
    text: []const u8, // TEXT, VARCHAR, CHAR, UUID, DATE, TIMESTAMPTZ
    numeric: []const u8, // NUMERIC - keep as decimal string
    jsonb: []const u8, // JSONB - as JSON string
    array: []const u8, // ARRAY - as JSON string
    bytea: []const u8, // BYTEA - raw bytes or hex/base64 encoded
};

/// Decoded column (name + value pair)
/// Used instead of HashMap for better performance - we only iterate, never lookup by key
pub const Column = struct {
    name: []const u8,
    value: DecodedValue,
};

/// Decodes a scalar of raw bytes based on the PostgreSQL type OID (BINARY format).
///
/// Decodes binary-encoded column data from PostgreSQL when using
/// START_REPLICATION with binary 'true'.
pub fn decodeBinColumnData(
    allocator: std.mem.Allocator,
    type_id: u32,
    raw_bytes: []const u8,
) !DecodedValue {
    const oid: PgOid = @enumFromInt(type_id);

    return switch (oid) {
        // --- Fixed-Width Numeric Types (Binary format) ---
        .BOOL => {
            if (raw_bytes.len != 1) return error.InvalidDataLength;
            return .{ .boolean = raw_bytes[0] != 0 };
        },

        .INT2 => {
            if (raw_bytes.len != 2) return error.InvalidDataLength;
            const val = std.mem.readInt(i16, raw_bytes[0..2], .big);
            return .{ .int32 = @intCast(val) };
        },

        .INT4 => {
            if (raw_bytes.len != 4) return error.InvalidDataLength;
            const val = std.mem.readInt(i32, raw_bytes[0..4], .big);
            return .{ .int32 = val };
        },

        .INT8 => {
            if (raw_bytes.len != 8) return error.InvalidDataLength;
            const val = std.mem.readInt(i64, raw_bytes[0..8], .big);
            return .{ .int64 = val };
        },

        .FLOAT4 => {
            if (raw_bytes.len != 4) return error.InvalidDataLength;
            const bits = std.mem.readInt(u32, raw_bytes[0..4], .big);
            const val: f32 = @bitCast(bits);
            return .{ .float64 = @floatCast(val) };
        },

        .FLOAT8 => {
            if (raw_bytes.len != 8) return error.InvalidDataLength;
            const bits = std.mem.readInt(u64, raw_bytes[0..8], .big);
            const val: f64 = @bitCast(bits);
            return .{ .float64 = val };
        },

        // --- Date/Time Types (Binary format) ---

        .DATE => {
            if (raw_bytes.len != 4) return error.InvalidDataLength;
            const pg_days = std.mem.readInt(i32, raw_bytes[0..4], .big);
            // PostgreSQL epoch: 2000-01-01
            // Convert to Unix epoch (1970-01-01)

            const PG_EPOCH_DAYS: i64 = 10957;
            const unix_days = @as(i64, pg_days) + PG_EPOCH_DAYS;
            // Use efficient civil calendar algorithm
            const date = utils.civilFromDays(unix_days);

            return .{
                // Format as YYYY-MM-DD (always 10 chars)
                .text = try std.fmt.allocPrint(
                    allocator,
                    "{d:0>4}-{d:0>2}-{d:0>2}",
                    .{ @as(u32, @intCast(date.year)), date.month, date.day },
                ),
            };
        },

        // ISO 8601 format: 2025-10-26T10:00:00.000000Z
        .TIMESTAMP, .TIMESTAMPTZ => {
            if (raw_bytes.len != 8) return error.InvalidDataLength;
            const microseconds = std.mem.readInt(i64, raw_bytes[0..8], .big);

            const PG_EPOCH_SECONDS: i64 = 946684800;
            const total_seconds = PG_EPOCH_SECONDS + @divFloor(microseconds, 1_000_000);
            const remaining_micros: u32 = @intCast(@abs(@mod(microseconds, 1_000_000)));

            const unix_days = @divFloor(total_seconds, 86400);
            const date = utils.civilFromDays(unix_days);

            // Convert to days for date calculation
            const day_seconds = @mod(total_seconds, 86400);
            const hour = @divFloor(day_seconds, 3600);
            const minute = @divFloor(@mod(day_seconds, 3600), 60);
            const second = @mod(day_seconds, 60);

            return .{ .text = try std.fmt.allocPrint(
                allocator,
                "{d:0>4}-{d:0>2}-{d:0>2}T{d:0>2}:{d:0>2}:{d:0>2}.{d:0>6}Z",
                .{
                    @as(u32, @intCast(date.year)),
                    date.month,
                    date.day,
                    @as(u8, @intCast(hour)),
                    @as(u8, @intCast(minute)),
                    @as(u8, @intCast(second)),
                    remaining_micros,
                },
            ) };
        },

        // --- UUID (Binary format - 16 bytes) ---
        .UUID => {
            if (raw_bytes.len != 16) return error.InvalidDataLength;
            return .{ .text = try formatUUID(allocator, raw_bytes) };
        },

        // --- Variable-Length Text Types (Binary format is still text) ---
        .TEXT,
        .VARCHAR,
        .BPCHAR,
        .JSON,
        => {
            // raw_bytes is from readBytes() which uses arena_allocator
            // We need to dupe with main_allocator so it survives arena cleanup
            const owned = try allocator.dupe(u8, raw_bytes);
            return .{ .text = owned };
        },
        .BYTEA => {
            // raw_bytes is from readBytes() which uses arena_allocator
            // We need to dupe with main_allocator so it survives arena cleanup
            const owned = try allocator.dupe(u8, raw_bytes);
            return .{ .bytea = owned };
        },

        // --- NUMERIC (Binary format - use numeric.zig) ---
        .NUMERIC => {
            const result = try numeric_mod.parseNumeric(allocator, raw_bytes);
            // parseNumeric returns NumericResult { .slice, .allocated }
            // where .slice is a sub-slice of .allocated pointing to the actual data
            //
            // We dupe the slice and free the original buffer
            const owned = try allocator.dupe(u8, result.slice);
            allocator.free(result.allocated);
            return .{ .numeric = owned };
        },

        // // --- JSON (text format even in binary mode) ---
        // .JSON => {
        //     // PostgreSQL sends JSON as plain text even when binary format is requested
        //     const owned = try allocator.dupe(u8, raw_bytes);
        //     return .{ .jsonb = owned }; // Use jsonb field for compatibility
        // },

        // --- JSONB (binary format is version byte + JSON text) ---
        .JSONB => {
            // PostgreSQL JSONB v1 format: version byte (0x01) + plain JSON text
            // No complex parsing needed - just skip the version byte
            if (raw_bytes.len < 1) return error.InvalidDataLength;
            const version = raw_bytes[0];
            if (version != 1) return error.UnsupportedJsonbVersion;

            // Skip version byte, return JSON text
            const owned = try allocator.dupe(u8, raw_bytes[1..]);
            return .{ .jsonb = owned };
        },

        // --- Array Types (Binary format - use array.zig) ---
        .ARRAY_INT2, .ARRAY_INT4, .ARRAY_INT8, .ARRAY_TEXT, .ARRAY_VARCHAR, .ARRAY_FLOAT4, .ARRAY_FLOAT8, .ARRAY_BOOL, .ARRAY_JSONB, .ARRAY_NUMERIC, .ARRAY_UUID, .ARRAY_TIMESTAMPTZ => {
            const parsed = try array_mod.parseArray(allocator, raw_bytes);
            defer parsed.deinit(allocator);

            // Convert ArrayResult to PostgreSQL text format: {1,2,3}
            const text_repr = try arrayToText(allocator, parsed);
            return .{ .array = text_repr };
        },

        // --- Handle ENUMs and unknown types gracefully ---
        else => {
            // ENUMs and other user-defined types are sent as text in binary format
            // Treat them as TEXT and log a warning for visibility
            log.warn("Unknown type OID {d}, treating as text", .{type_id});
            const owned = try allocator.dupe(u8, raw_bytes);
            return .{ .text = owned };
        },
    };
}

/// Convert ArrayResult to PostgreSQL array style text format '{{1,2}, {3,4}}'
pub fn arrayToText(allocator: std.mem.Allocator, arr: ArrayResult) ![]u8 {
    var res: std.ArrayList(u8) = .empty;
    _ = try writeArrayRecursive(&res, allocator, arr, 0, 0);
    return res.toOwnedSlice(allocator);
}

fn writeArrayRecursive(
    out: *std.ArrayList(u8),
    allocator: std.mem.Allocator,
    arr: ArrayResult,
    dim: usize,
    index: usize,
) !usize {
    const dims = arr.dimensions;
    const ndim = arr.ndim;

    const count: usize = @intCast(dims[dim]);

    var flat_index = index;

    try out.append(allocator, '{');

    var i: usize = 0;
    while (i < count) : (i += 1) {
        if (i > 0) try out.append(allocator, ',');

        if (dim + 1 == ndim) {
            // leaf element
            const elem = arr.elements[flat_index];
            switch (elem) {
                .null_value => try out.appendSlice(allocator, "NULL"),
                .data => blk: {
                    const pg_oid: PgOid = @enumFromInt(arr.element_oid);

                    // BYTEA: \xHEXSTRING format
                    if (pg_oid == PgOid.BYTEA) {
                        const hex = try elementToText(allocator, pg_oid, elem.data);
                        defer allocator.free(hex);
                        try out.appendSlice(allocator, "\\x");
                        try out.appendSlice(allocator, hex);
                        break :blk;
                    }

                    // UUID: formatted string (no quotes needed in array)
                    if (pg_oid == PgOid.UUID) {
                        const uuid_str = try elementToText(allocator, pg_oid, elem.data);
                        defer allocator.free(uuid_str);
                        try out.appendSlice(allocator, uuid_str);
                        break :blk;
                    }

                    // TEXT/VARCHAR: requires quotes and escaping
                    if (pg_oid == PgOid.TEXT or pg_oid == PgOid.VARCHAR) {
                        const txt = try elementToText(allocator, pg_oid, elem.data);
                        defer allocator.free(txt);

                        try out.append(allocator, '"');
                        for (txt) |c| {
                            if (c == '"' or c == '\\')
                                try out.append(allocator, '\\');
                            try out.append(allocator, c);
                        }
                        try out.append(allocator, '"');
                        break :blk;
                    }

                    // All other types (INT, FLOAT, BOOL, etc.): just append as-is
                    const s = try elementToText(allocator, pg_oid, elem.data);
                    defer allocator.free(s);
                    try out.appendSlice(allocator, s);
                },
            }
            flat_index += 1;
        } else {
            // recurse into next dimension
            flat_index = try writeArrayRecursive(out, allocator, arr, dim + 1, flat_index);
        }
    }

    try out.append(allocator, '}');

    return flat_index;
}

// Array element decoding
fn elementToText(allocator: std.mem.Allocator, pg_oid: PgOid, data: []const u8) ![]u8 {
    // const pgiod: PgOid = @enumFromInt(oid_int);
    switch (pg_oid) {
        .INT2 => {
            const v = std.mem.readInt(i16, data[0..2], .big);
            return std.fmt.allocPrint(allocator, "{}", .{v});
        },
        .INT4 => {
            const v = std.mem.readInt(i32, data[0..4], .big);
            return std.fmt.allocPrint(allocator, "{}", .{v});
        },
        .INT8 => {
            const v = std.mem.readInt(i64, data[0..8], .big);
            return std.fmt.allocPrint(allocator, "{}", .{v});
        },
        .FLOAT4 => {
            const bits = std.mem.readInt(u32, data[0..4], .big);
            const v: f32 = @bitCast(bits);
            return std.fmt.allocPrint(allocator, "{}", .{v});
        },
        .FLOAT8 => {
            const bits = std.mem.readInt(u64, data[0..8], .big);
            const v: f64 = @bitCast(bits);
            return std.fmt.allocPrint(allocator, "{}", .{v});
        },
        .TEXT, .VARCHAR => return allocator.dupe(u8, data),
        .BYTEA => {
            // Encode as lowercase hex format
            return formatBYTEA(allocator, data);
        },
        .UUID => {
            return try formatUUID(allocator, data);
        },
        else => return allocator.dupe(u8, data),
    }
}

fn formatBYTEA(allocator: std.mem.Allocator, data: []const u8) ![]u8 {
    var hex_buf = try allocator.alloc(u8, data.len * 2);
    var pos: usize = 0;
    for (data) |b| {
        const hi: u8 = @intCast((b >> 4) & 0xF);
        const lo: u8 = @intCast(b & 0xF);
        hex_buf[pos] = if (hi < 10) '0' + hi else 'a' + hi - 10;
        pos += 1;
        hex_buf[pos] = if (lo < 10) '0' + lo else 'a' + lo - 10;
        pos += 1;
    }
    return hex_buf;
}

// 4-2-2-2-6 hex digit format with dashes, 128bits = 16x8 bits
fn formatUUID(allocator: std.mem.Allocator, data: []const u8) ![]u8 {
    // UUID is 16 bytes
    var res = try allocator.alloc(u8, 36);
    var pos: usize = 0;

    for (data, 0..) |b, i| {
        if (i == 4 or i == 6 or i == 8 or i == 10) {
            res[pos] = '-';
            pos += 1;
        }
        // Write byte as two hex digits
        utils.byteToHex(res[pos..][0..2], b);
        pos += 2;
    }

    return res;
}

pub const TupleData = struct {
    cols: []?[]u8,

    pub fn deinit(self: *TupleData, allocator: std.mem.Allocator) void {
        for (self.cols) |col| {
            if (col) |data| {
                allocator.free(data);
            }
        }
        allocator.free(self.cols);
    }
};

pub fn decodeTuple(
    allocator: std.mem.Allocator,
    tuple: TupleData,
    columns: []const RelationMessage.ColumnInfo,
) !std.ArrayList(Column) {
    var decoded_columns = std.ArrayList(Column){};
    errdefer decoded_columns.deinit(allocator);

    if (tuple.cols.len != columns.len) return error.ColumnMismatch;

    // Pre-allocate capacity for all columns
    try decoded_columns.ensureTotalCapacity(allocator, columns.len);

    for (columns, tuple.cols) |col_info, col_data| {
        if (col_data) |raw_bytes| {
            // Log raw text value for debugging
            log.debug("Column '{s}' (OID={d}): '{s}'", .{ col_info.name, col_info.type_id, raw_bytes });

            const decoded_value = decodeBinColumnData(allocator, col_info.type_id, raw_bytes) catch |err| {
                log.err("Failed to decode column '{s}' (type_id={d}, bytes={d}): {}", .{ col_info.name, col_info.type_id, raw_bytes.len, err });
                return err;
            };
            // Column name points to RelationMessage which has stable lifetime
            // No need to duplicate - it lives for the entire relation cache
            decoded_columns.appendAssumeCapacity(Column{
                .name = col_info.name,
                .value = decoded_value,
            });
        } else {
            // Handle NULL values
            log.debug("Column '{s}' is NULL", .{col_info.name});
            decoded_columns.appendAssumeCapacity(Column{
                .name = col_info.name,
                .value = .null,
            });
        }
    }
    return decoded_columns;
}

pub const RelationMessage = struct {
    relation_id: u32,
    namespace: []const u8,
    name: []const u8,
    replica_identity: u8,
    columns: []ColumnInfo,

    pub const ColumnInfo = struct {
        flags: u8,
        name: []const u8,
        type_id: u32,
        type_modifier: i32,
    };

    pub fn deinit(self: *RelationMessage, allocator: std.mem.Allocator) void {
        allocator.free(self.namespace);
        allocator.free(self.name);
        for (self.columns) |col| {
            allocator.free(col.name);
        }
        allocator.free(self.columns);
    }

    pub fn clone(self: *const RelationMessage, allocator: std.mem.Allocator) !*RelationMessage {
        var out = try allocator.create(RelationMessage);
        errdefer allocator.destroy(out);

        out.* = self.*;

        // Deep copy namespace
        out.namespace = try allocator.dupe(u8, self.namespace);
        errdefer allocator.free(out.namespace);

        // Deep copy name
        out.name = try allocator.dupe(u8, self.name);
        errdefer allocator.free(out.name);

        // Deep copy columns array
        out.columns = try allocator.alloc(ColumnInfo, self.columns.len);
        errdefer allocator.free(out.columns);

        for (self.columns, 0..) |col, i| {
            const cloned_col_name = try allocator.dupe(u8, col.name);
            errdefer {
                // Free previously cloned column names on error
                for (out.columns[0..i]) |c| {
                    allocator.free(c.name);
                }
            }
            out.columns[i] = ColumnInfo{
                .flags = col.flags,
                .name = cloned_col_name,
                .type_id = col.type_id,
                .type_modifier = col.type_modifier,
            };
        }

        return out;
    }
};

pub const InsertMessage = struct {
    relation_id: u32,
    tuple_data: TupleData,

    pub fn deinit(self: *InsertMessage, allocator: std.mem.Allocator) void {
        self.tuple_data.deinit(allocator);
    }
};

pub const UpdateMessage = struct {
    relation_id: u32,
    old_tuple: ?TupleData,
    new_tuple: TupleData,

    pub fn deinit(self: *UpdateMessage, allocator: std.mem.Allocator) void {
        if (self.old_tuple) |*old| old.deinit(allocator);

        self.new_tuple.deinit(allocator);
    }
};

pub const DeleteMessage = struct {
    relation_id: u32,
    old_tuple: TupleData,

    pub fn deinit(self: *DeleteMessage, allocator: std.mem.Allocator) void {
        self.old_tuple.deinit(allocator);
    }
};

pub const OriginMessage = struct {
    commit_lsn: u64,
    name: []const u8,
};

pub const TypeMessage = struct {
    type_id: u32,
    namespace: []const u8,
    name: []const u8,
};

pub const TruncateMessage = struct {
    options: u8,
    relation_ids: []u32,
};

/// Parser state for decoding pgoutput messages
///
/// Handles reading from a byte slice and parsing messages sequentially
pub const Parser = struct {
    allocator: std.mem.Allocator,
    data: []const u8,
    pos: usize,

    pub fn init(allocator: std.mem.Allocator, data: []const u8) Parser {
        return .{
            .allocator = allocator,
            .data = data,
            .pos = 0,
        };
    }

    /// Parse the next pgoutput message from the data based on message type
    ///
    /// Source: https://www.postgresql.org/docs/current/protocol-logicalrep-message-formats.html#PROTOCOL-LOGICALREP-MESSAGE-FORMATS-MESSAGE-TYPE
    pub fn parse(self: *Parser) !PgOutputMessage {
        if (self.pos >= self.data.len) {
            return error.EndOfData;
        }

        const msg_type = self.data[self.pos];
        self.pos += 1;

        return switch (msg_type) {
            'B' => .{ .begin = try self.parseBegin() },
            'C' => .{ .commit = try self.parseCommit() },
            'R' => .{ .relation = try self.parseRelation() },
            'I' => .{ .insert = try self.parseInsert() },
            'U' => .{ .update = try self.parseUpdate() },
            'D' => .{ .delete = try self.parseDelete() },
            'O' => .{ .origin = try self.parseOrigin() },
            'Y' => .{ .type = try self.parseType() },
            'T' => .{ .truncate = try self.parseTruncate() },
            else => {
                log.warn("Unknown pgoutput message type: {c} (0x{x})", .{ msg_type, msg_type });
                return error.UnknownMessageType;
            },
        };
    }

    // https://www.postgresql.org/docs/current/protocol-logicalrep-message-formats.html#PROTOCOL-LOGICALREP-MESSAGE-FORMATS-BEGIN
    fn parseBegin(self: *Parser) !BeginMessage {
        const final_lsn = try self.readU64();
        const timestamp = try self.readI64();
        const xid = try self.readU32();

        return BeginMessage{
            .final_lsn = final_lsn,
            .timestamp = timestamp,
            .xid = xid,
        };
    }

    // https://www.postgresql.org/docs/current/protocol-logicalrep-message-formats.html#PROTOCOL-LOGICALREP-MESSAGE-FORMATS-COMMIT
    fn parseCommit(self: *Parser) !CommitMessage {
        const flags = try self.readU8();
        const commit_lsn = try self.readU64();
        const end_lsn = try self.readU64();
        const timestamp = try self.readI64();

        return CommitMessage{
            .flags = flags,
            .commit_lsn = commit_lsn,
            .end_lsn = end_lsn,
            .timestamp = timestamp,
        };
    }

    // https://www.postgresql.org/docs/current/protocol-logicalrep-message-formats.html#PROTOCOL-LOGICALREP-MESSAGE-FORMATS-RELATION
    fn parseRelation(self: *Parser) !RelationMessage {
        const relation_id = try self.readU32();
        const namespace = try self.readString();
        const name = try self.readString();
        const replica_identity = try self.readU8();
        const num_columns = try self.readU16();

        var columns_ptr = try self.allocator.alloc(
            RelationMessage.ColumnInfo,
            num_columns,
        );
        errdefer self.allocator.free(columns_ptr);

        var i: usize = 0;
        while (i < num_columns) : (i += 1) {
            const flags = try self.readU8();
            const col_name = try self.readString();
            const type_id = try self.readU32();
            const type_modifier = try self.readI32();

            columns_ptr[i] = .{
                .flags = flags,
                .name = col_name,
                .type_id = type_id,
                .type_modifier = type_modifier,
            };
        }

        return RelationMessage{
            .relation_id = relation_id,
            .namespace = namespace,
            .name = name,
            .replica_identity = replica_identity,
            .columns = columns_ptr[0..num_columns],
        };
    }

    // https://www.postgresql.org/docs/current/protocol-logicalrep-message-formats.html#PROTOCOL-LOGICALREP-MESSAGE-FORMATS-INSERT
    fn parseInsert(self: *Parser) !InsertMessage {
        const relation_id = try self.readU32();
        const tuple_type = try self.readU8();

        if (tuple_type != 'N') {
            log.warn("Expected 'N' (new tuple), got: {c}", .{tuple_type});
            return error.InvalidTupleType;
        }

        const tuple_data = try self.parseTupleData();

        return InsertMessage{
            .relation_id = relation_id,
            .tuple_data = tuple_data,
        };
    }

    // https://www.postgresql.org/docs/current/protocol-logicalrep-message-formats.html#PROTOCOL-LOGICALREP-MESSAGE-FORMATS-UPDATE
    fn parseUpdate(self: *Parser) !UpdateMessage {
        const relation_id = try self.readU32();
        const key_or_old = try self.readU8();

        var old_tuple: ?TupleData = null;

        if (key_or_old == 'K' or key_or_old == 'O') {
            // 'K' = old key tuple, 'O' = old full tuple
            old_tuple = try self.parseTupleData();

            // After old tuple, next byte MUST be 'N' for new tuple
            const new_marker = try self.readU8();
            if (new_marker != 'N') {
                log.warn("Expected 'N' (new tuple) after old tuple, got: {c} (0x{x})", .{ new_marker, new_marker });
                return error.InvalidTupleType;
            }
        } else if (key_or_old != 'N') {
            // If not 'K', 'O', or 'N', this is invalid
            log.warn("Invalid UPDATE tuple marker: {c} (0x{x}), expected 'K', 'O', or 'N'", .{ key_or_old, key_or_old });
            return error.InvalidTupleType;
        }
        // else: key_or_old == 'N', no old tuple, proceed directly to new tuple

        const new_tuple = try self.parseTupleData();

        return UpdateMessage{
            .relation_id = relation_id,
            .old_tuple = old_tuple,
            .new_tuple = new_tuple,
        };
    }

    // https://www.postgresql.org/docs/current/protocol-logicalrep-message-formats.html#PROTOCOL-LOGICALREP-MESSAGE-FORMATS-DELETE
    fn parseDelete(self: *Parser) !DeleteMessage {
        const relation_id = try self.readU32();
        const key_or_old = try self.readU8();

        if (key_or_old != 'K' and key_or_old != 'O') {
            log.warn("Expected 'K' or 'O', got: {c}", .{key_or_old});
            return error.InvalidTupleType;
        }

        const old_tuple = try self.parseTupleData();

        return DeleteMessage{
            .relation_id = relation_id,
            .old_tuple = old_tuple,
        };
    }

    // https://www.postgresql.org/docs/current/protocol-logicalrep-message-formats.html#PROTOCOL-LOGICALREP-MESSAGE-FORMATS-ORIGIN
    fn parseOrigin(self: *Parser) !OriginMessage {
        const commit_lsn = try self.readU64();
        const name = try self.readString();

        return OriginMessage{
            .commit_lsn = commit_lsn,
            .name = name,
        };
    }

    // https://www.postgresql.org/docs/current/protocol-logicalrep-message-formats.html#PROTOCOL-LOGICALREP-MESSAGE-FORMATS-TYPE
    fn parseType(self: *Parser) !TypeMessage {
        const type_id = try self.readU32();
        const namespace = try self.readString();
        const name = try self.readString();

        return TypeMessage{
            .type_id = type_id,
            .namespace = namespace,
            .name = name,
        };
    }

    // https://www.postgresql.org/docs/current/protocol-logicalrep-message-formats.html#PROTOCOL-LOGICALREP-MESSAGE-FORMATS-TRUNCATE
    fn parseTruncate(self: *Parser) !TruncateMessage {
        const options = try self.readU8();
        const num_relations = try self.readU32();

        var relation_ids = try self.allocator.alloc(u32, num_relations);
        errdefer self.allocator.free(relation_ids);

        var i: usize = 0;
        while (i < num_relations) : (i += 1) {
            relation_ids[i] = try self.readU32();
        }

        return TruncateMessage{
            .options = options,
            .relation_ids = relation_ids,
        };
    }

    // https://www.postgresql.org/docs/current/protocol-logicalrep-message-formats.html#PROTOCOL-LOGICALREP-MESSAGE-FORMATS-TUPLEDATA
    fn parseTupleData(self: *Parser) !TupleData {
        const num_columns = try self.readU16();
        const ncols: usize = @intCast(num_columns);
        var cols_ptr = try self.allocator.alloc(?[]u8, ncols);
        // errdefer self.allocator.free(cols_ptr);
        var clean_cols: bool = true;

        defer {
            if (clean_cols) {
                // free each owned val that was allocated
                for (cols_ptr[0..ncols]) |col| {
                    if (col) |c| self.allocator.free(c);
                }
                self.allocator.free(cols_ptr);
            }
        }

        var i: usize = 0;
        while (i < ncols) : (i += 1) {
            const kind_byte = try self.readU8();
            switch (kind_byte) {
                'n' => {
                    cols_ptr[i] = null;
                },
                'u' => {
                    cols_ptr[i] = null;
                },
                't' => {
                    // text format
                    const len = try self.readU32();
                    const owned = try self.readBytesOwned(len);
                    cols_ptr[i] = owned;
                },
                'b' => {
                    // binary format
                    const len = try self.readU32();
                    const owned = try self.readBytesOwned(len);
                    cols_ptr[i] = owned;
                },
                else => {
                    // unknown marker
                    return error.UnknownColumnType;
                },
            }
        }
        // success path, do not deallocate cols_ptr
        clean_cols = false;
        return TupleData{ .cols = cols_ptr[0..ncols] };
    }

    /// Read a single byte
    fn readU8(self: *Parser) !u8 {
        if (self.pos >= self.data.len) return error.UnexpectedEndOfData;
        const val = self.data[self.pos];
        self.pos += 1;
        return val;
    }

    /// Read a 16-bit unsigned integer in big-endian order
    fn readU16(self: *Parser) !u16 {
        if (self.pos + 2 > self.data.len) return error.UnexpectedEndOfData;
        const array_ptr: *const [2]u8 = self.data[self.pos..][0..2];
        const val = std.mem.readInt(u16, array_ptr, .big);
        self.pos += 2;
        return val;
    }

    /// Read a 32-bit unsigned integer in big-endian order
    fn readU32(self: *Parser) !u32 {
        if (self.pos + 4 > self.data.len) return error.UnexpectedEndOfData;
        const array_ptr = self.data[self.pos..][0..4];
        const val = std.mem.readInt(u32, array_ptr, .big);
        self.pos += 4;
        return val;
    }

    /// Read a 32-bit signed integer in big-endian order
    fn readI32(self: *Parser) !i32 {
        if (self.pos + 4 > self.data.len) return error.UnexpectedEndOfData;
        const array_ptr = self.data[self.pos..][0..4];
        const val = std.mem.readInt(i32, array_ptr, .big);
        self.pos += 4;
        return val;
    }

    /// Read a 64-bit unsigned integer in big-endian order
    fn readU64(self: *Parser) !u64 {
        if (self.pos + 8 > self.data.len) return error.UnexpectedEndOfData;
        const array_ptr = self.data[self.pos..][0..8];
        const val = std.mem.readInt(u64, array_ptr, .big);
        self.pos += 8;
        return val;
    }

    /// Read a 64-bit signed integer in big-endian order
    fn readI64(self: *Parser) !i64 {
        if (self.pos + 8 > self.data.len) return error.UnexpectedEndOfData;
        const array_ptr = self.data[self.pos..][0..8];
        const val = std.mem.readInt(i64, array_ptr, .big);
        self.pos += 8;
        return val;
    }

    /// Read a null-terminated string
    ///
    /// Caller is responsible for freeing the returned string
    fn readString(self: *Parser) ![]const u8 {
        const start = self.pos;
        while (self.pos < self.data.len and self.data[self.pos] != 0) {
            self.pos += 1;
        }

        if (self.pos >= self.data.len) return error.UnexpectedEndOfData;

        const str = self.data[start..self.pos];
        self.pos += 1; // Skip null terminator

        return try self.allocator.dupe(u8, str);
    }

    /// Read `len` bytes and return an owned byte mutable slice
    ///
    /// Caller is responsible for freeing the returned byte slice
    fn readBytesOwned(self: *Parser, len: u32) ![]u8 {
        const len_usize: usize = @intCast(len);
        const end = self.pos + len_usize;
        if (end > self.data.len) return error.UnexpectedEndOfData;

        const bytes = self.data[self.pos..end];
        self.pos = end;

        const dst = try self.allocator.alloc(u8, len_usize);
        @memcpy(dst, bytes);
        return dst;
    }
};

// === Tests ===

const parseArray = array_mod.parseArray;

inline fn writeIntBE(comptime T: type, buf: []u8, value: T, endian: std.builtin.Endian) void {
    const ptr: *[@divExact(@typeInfo(T).int.bits, 8)]u8 = @ptrCast(buf.ptr);
    std.mem.writeInt(T, ptr, value, endian);
}

test "1D int4 → {1,2,3,4}" {
    const alloc = std.testing.allocator;

    const buf = [_]u8{
        0, 0, 0, 1, // ndim
        0, 0, 0, 0, // flags
        0, 0, 0, 23, // element_oid = INT4
        0, 0, 0, 4, // dim1_len
        0, 0, 0, 1, // dim1_lower
        0, 0, 0, 4,
        0, 0, 0, 1,
        0, 0, 0, 4,
        0, 0, 0, 2,
        0, 0, 0, 4,
        0, 0, 0, 3,
        0, 0, 0, 4,
        0, 0, 0, 4,
    };

    var arr = try parseArray(alloc, &buf);
    defer arr.deinit(alloc);

    const txt = try arrayToText(alloc, arr);
    defer alloc.free(txt);

    try std.testing.expectEqualStrings("{1,2,3,4}", txt);
}

test "2D int4 → {{1,2},{3,4}}" {
    const alloc = std.testing.allocator;

    const buf = [_]u8{
        0, 0, 0, 2, // ndim = 2
        0, 0, 0, 0,
        0, 0, 0, 23, // INT4
        0, 0, 0, 2, 0, 0, 0, 1, // dim1_len, lower
        0, 0, 0, 2, 0, 0, 0, 1, // dim2_len, lower

        // (1,1)
        0, 0, 0, 4, 0, 0, 0, 1,
        // (1,2)
        0, 0, 0, 4, 0, 0, 0, 2,
        // (2,1)
        0, 0, 0, 4, 0, 0, 0, 3,
        // (2,2)
        0, 0, 0, 4, 0, 0, 0, 4,
    };

    var arr = try parseArray(alloc, &buf);
    defer arr.deinit(alloc);

    const txt = try arrayToText(alloc, arr);
    defer alloc.free(txt);

    try std.testing.expectEqualStrings("{{1,2},{3,4}}", txt);
}

test "3D int4 → {{{1,2},{3,4}},{{5,6},{7,8}}}" {
    const alloc = std.testing.allocator;

    // 3D: dims = [2,2,2], flattened [1..8]
    const buf = blk: {
        var b: [136]u8 = undefined;
        var pos: usize = 0;

        // ndim = 3
        writeIntBE(i32, b[pos .. pos + 4], 3, .big);
        pos += 4;
        // flags
        writeIntBE(i32, b[pos .. pos + 4], 0, .big);
        pos += 4;
        // oid INT4 = 23
        writeIntBE(u32, b[pos .. pos + 4], 23, .big);
        pos += 4;

        // dim1 len=2, lower=1
        writeIntBE(i32, b[pos .. pos + 4], 2, .big);
        pos += 4;
        writeIntBE(i32, b[pos .. pos + 4], 1, .big);
        pos += 4;

        // dim2 len=2, lower=1
        writeIntBE(i32, b[pos .. pos + 4], 2, .big);
        pos += 4;
        writeIntBE(i32, b[pos .. pos + 4], 1, .big);
        pos += 4;

        // dim3 len=2, lower=1
        writeIntBE(i32, b[pos .. pos + 4], 2, .big);
        pos += 4;
        writeIntBE(i32, b[pos .. pos + 4], 1, .big);
        pos += 4;

        var x: i32 = 1;
        for (0..8) |_| {
            writeIntBE(i32, b[pos .. pos + 4], 4, .big);
            pos += 4;
            writeIntBE(i32, b[pos .. pos + 4], x, .big);
            pos += 4;
            x += 1;
        }

        break :blk b;
    };

    var arr = try parseArray(alloc, &buf);
    defer arr.deinit(alloc);

    const txt = try arrayToText(alloc, arr);
    defer alloc.free(txt);

    try std.testing.expectEqualStrings(
        "{{{1,2},{3,4}},{{5,6},{7,8}}}",
        txt,
    );
}

test "NULL elements → {1,NULL,3}" {
    const alloc = std.testing.allocator;

    const buf = [_]u8{
        0,   0,   0,   1, // ndim = 1
        0,   0,   0,   0, // flags
        0,   0,   0,   23, // INT4
        0,   0,   0,   3, // dim_len = 3 elements
        0,   0,   0,   1, // dim_lower = 1
        // Element 1: length=4, value=1
        0,   0,   0,   4,
        0,   0,   0,   1,
        // Element 2: length=-1 (NULL)
        255, 255, 255, 255,
        // Element 3: length=4, value=3
        0,   0,   0,   4,
        0,   0,   0,   3,
    };

    var arr = try parseArray(alloc, &buf);
    defer arr.deinit(alloc);

    const txt = try arrayToText(alloc, arr);
    defer alloc.free(txt);

    try std.testing.expectEqualStrings("{1,NULL,3}", txt);
}

test "TEXT array escaping → {\"a\",\"b c\",\"d\\\"e\"}" {
    const alloc = std.testing.allocator;

    const buf = [_]u8{
        0, 0, 0, 1,
        0, 0, 0, 0,
        0,   0,   0,   25, // TEXT
        0,   0,   0,   3,
        0,   0,   0,   1,
        // "a"
        0,   0,   0,   1,
        'a',
        // "b c"
        0,   0,   0,
        3,   'b', ' ', 'c',
        // "d" e → d\"e
        0,   0,   0,   3,
        'd', '"', 'e',
    };

    var arr = try parseArray(alloc, &buf);
    defer arr.deinit(alloc);

    const txt = try arrayToText(alloc, arr);
    defer alloc.free(txt);

    try std.testing.expectEqualStrings(
        "{\"a\",\"b c\",\"d\\\"e\"}",
        txt,
    );
}

test "BYTEA array → {\\xdeadbeef,\\x01020304}" {
    const alloc = std.testing.allocator;

    const buf = [_]u8{
        0, 0, 0, 1, // ndim
        0, 0, 0, 0,
        0,    0,    0,    17, // BYTEA
        0,    0,    0,    2,
        0,    0,    0,    1,

        // element 1: 4 bytes
        0,    0,    0,    4,
        0xDE, 0xAD, 0xBE, 0xEF,

        // element 2: 4 bytes
        0,    0,    0,    4,
        0x01, 0x02, 0x03, 0x04,
    };

    var arr = try parseArray(alloc, &buf);
    defer arr.deinit(alloc);

    const txt = try arrayToText(alloc, arr);
    defer alloc.free(txt);

    try std.testing.expectEqualStrings(
        "{\\xdeadbeef,\\x01020304}",
        txt,
    );
}

test "FLOAT8 array → {1.5,2.25,3.75}" {
    const alloc = std.testing.allocator;

    var buf: [4 + 4 + 4 + 8 + 8 + 8 + 8 + 8 + 8]u8 = undefined;
    var pos: usize = 0;

    // ndim = 1
    writeIntBE(i32, buf[pos .. pos + 4], 1, .big);
    pos += 4;
    writeIntBE(i32, buf[pos .. pos + 4], 0, .big);
    pos += 4;
    writeIntBE(u32, buf[pos .. pos + 4], @intFromEnum(PgOid.FLOAT8), .big);
    pos += 4;

    writeIntBE(i32, buf[pos .. pos + 4], 3, .big);
    pos += 4;
    writeIntBE(i32, buf[pos .. pos + 4], 1, .big);
    pos += 4;

    const floats = [_]f64{ 1.5, 2.25, 3.75 };

    for (floats) |f| {
        writeIntBE(i32, buf[pos .. pos + 4], 8, .big);
        pos += 4;
        const bits: u64 = @bitCast(f);
        writeIntBE(u64, buf[pos .. pos + 8], bits, .big);
        pos += 8;
    }

    var arr = try parseArray(alloc, &buf);
    defer arr.deinit(alloc);

    const txt = try arrayToText(alloc, arr);
    defer alloc.free(txt);

    try std.testing.expectEqualStrings("{1.5,2.25,3.75}", txt);
}

test "UUID array → {uuid1,uuid2}" {
    const alloc = std.testing.allocator;

    const uuid1 = [_]u8{ 0, 1, 2, 3, 4, 5, 6, 7, 8, 9, 0xAA, 0xBB, 0xCC, 0xDD, 0xEE, 0xFF };
    const uuid2 = [_]u8{ 0x10, 0x20, 0x30, 0x40, 0x50, 0x60, 0x70, 0x80, 0x90, 0xA0, 0xB0, 0xC0, 0xD0, 0xE0, 0xF0, 0xFF };

    var buf: [4 + 4 + 4 + 8 + 4 + 16 + 4 + 16]u8 = undefined;
    var pos: usize = 0;

    writeIntBE(i32, buf[pos .. pos + 4], 1, .big);
    pos += 4;
    writeIntBE(i32, buf[pos .. pos + 4], 0, .big);
    pos += 4;
    writeIntBE(u32, buf[pos .. pos + 4], @intFromEnum(PgOid.UUID), .big);
    pos += 4;
    writeIntBE(i32, buf[pos .. pos + 4], 2, .big);
    pos += 4;
    writeIntBE(i32, buf[pos .. pos + 4], 1, .big);
    pos += 4;

    writeIntBE(i32, buf[pos .. pos + 4], 16, .big);
    pos += 4;
    @memcpy(buf[pos .. pos + 16], &uuid1);
    pos += 16;

    writeIntBE(i32, buf[pos .. pos + 4], 16, .big);
    pos += 4;
    @memcpy(buf[pos .. pos + 16], &uuid2);
    pos += 16;

    var arr = try parseArray(alloc, buf[0..pos]);
    defer arr.deinit(alloc);

    const out = try arrayToText(alloc, arr);
    defer alloc.free(out);

    // PostgreSQL format
    try std.testing.expectEqualStrings(
        "{00010203-0405-0607-0809-aabbccddeeff,10203040-5060-7080-90a0-b0c0d0e0f0ff}",
        out,
    );
}
